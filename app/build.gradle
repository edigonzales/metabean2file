plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jreleaser' version '1.0.0'
}

group = 'ch.so.agi'
def myArtifactId = "meta2file-cli"

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
    options.compilerArgs.addAll(['--release', '11'])
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'org.postgresql:postgresql:42.3.4'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'org.freemarker:freemarker:2.3.31'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.testcontainers:testcontainers:1.17.2'
    testImplementation "org.testcontainers:junit-jupiter:1.17.2"
    testImplementation 'org.testcontainers:postgresql:1.17.2'

    implementation project(':lib')
}

application {
    mainClass = 'ch.so.agi.meta2file.appmain.Meta2File'
}

distributions {
    main {
        distributionBaseName = myArtifactId
    }
}

project.archivesBaseName = myArtifactId

startScripts {
   applicationName = myArtifactId
}

tasks.named('test') {
    useJUnitPlatform()
}

run {
    args = ["--dburl", "jdbc:postgresql://localhost:54321/edit", "--foo", "bar"]
    //args = ["--help"]
}

jreleaser {
    gitRootSearch = true
    
    project {
        name = myArtifactId
        description = 'Creates metadata markup files (html) and transfer files (INTERLIS).'
        website = 'https://agi.so.ch'
        authors = ['edigonzales', 'ojeker']
        license = 'MIT'
        docsUrl = 'https://github.com/sogis/meta2file'
    }
    release {
        github {
            owner = 'sogis'
            overwrite = true
            apiEndpoint = 'https://api.github.com'
        }
    }
    distributions {
        app {
            distributionType = 'SINGLE_JAR'
            artifact {
                path = "build/libs/meta2file-cli-${version}-all.jar"
            }
            java {
                groupId = 'ch.so.agi'
                artifactId = myArtifactId
                version = '11'
            }
        }
    }
}
